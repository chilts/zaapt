%# ----------------------------------------------------------------------------
<%doc>
    Note: $perms is not a required arg, so make sure the person editing this
    is allowed to.
</%doc>
%# ----------------------------------------------------------------------------
<%once>
    use Zaapt::Utils::Valid;
    use Zaapt::Utils::Conv;
</%once>
%# ----------------------------------------------------------------------------
<%args>
    $zaapt
    $id
    $sess
    $_act        => ''
    $a_firstname => ''
    $a_lastname  => ''
    $a_email     => ''
    $a_notify    => 1
    $a_password  => ''
    $a_repeat    => ''
</%args>
%# ----------------------------------------------------------------------------
<%init>
    my $model = $zaapt->get_model('Account');
    my $account = $model->sel_account({ a_id => $id });

    my $err_info = [];
    my $err_pass = [];

    if ( $_act eq 'update' ) {
        # turn notify into a boolean
        $a_notify = Zaapt::Utils::Conv::to_bool( $a_notify );

        my $empty = 0;
        foreach ( $a_firstname, $a_lastname, $a_email ) {
            Zaapt::Utils::Valid::has_content( $_ )
                or $empty++;
        }
        push @$err_info, "At least one field is blank, please enter all fields" if $empty;

        unless ( @$err_info ) {
            $model->upd_account({
                a_firstname => $a_firstname,
                a_lastname  => $a_lastname,
                a_email     => $a_email,
                a_notify    => $a_notify,
                a_id        => $id,
            });
            $m->comp(
                '/zaapt/common/refresh-session.mhtml',
                zaapt => $zaapt,
                id => $id,
                sess => $sess
            );
            $m->clear_buffer();
            $m->redirect('.');
            return;
        }
    }
    elsif ( $_act eq 'password' ) {
        # check the passwords are okay

        my $empty = 0;
        foreach ( $a_password, $a_repeat ) {
            $empty++ unless m{ \S }xms;
        }
        push @$err_pass, "At least one field is blank, please enter all fields" if $empty;

        unless ( $a_password eq $a_repeat ) {
            push @$err_pass, "Password and the repeat are not the same, please re-type";
        }

        unless ( length $a_password >= 6 ) {
            push @$err_pass, "Password must be at least 6 characters long.";
        }

        unless ( @$err_pass ) {
            $model->upd_password({
                a_password => $a_password,
                a_id       => $id,
            });
            $m->clear_buffer();
            $m->redirect('.');
            return;
        }
    }
</%init>
%# ----------------------------------------------------------------------------
<& user-edit.render.mhtml,
    values => $account,
    err_info => $err_info,
    err_pass => $err_pass
&>
%# ----------------------------------------------------------------------------
