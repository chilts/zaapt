%# ----------------------------------------------------------------------------
<%args>
    $zaapt
    $sess
    $domain
    $a_username => ''
    $a_password => ''
</%args>
%# ----------------------------------------------------------------------------
<%init>
    my $err;

    if ( $a_username ne '' and $a_password ne '' ) {
        # check the username and password properly
        my $model = $zaapt->get_model('Account');
        my $account = $model->sel_account_for_authentication({ a_username => $a_username, a_password => $a_password });
        if ( defined $account ) {

            # sign in
            $m->comp('/zaapt/model/session/session-sign-in.mas',
                sess_rh => $sess,
                dbh     => $zaapt->dbh,
                domain  => ".$domain",
                expires => undef,
            );

            # done all the cookie/session stuff, now the interesting part
            foreach ( qw( id username firstname lastname email admin logins last ) ) {
                $sess->{$_} = $account->{"a_$_"};
            }

            # get the permissions for this user sel_permission_all_for_account
            my $permissions = $model->sel_permission_all_for_account({ a_id => $account->{a_id} });
            $sess->{perms} = {};
            foreach my $permission ( @$permissions ) {
                $sess->{perms}{$permission->{p_id}} = $permission->{p_name};
                if ( $permission->{p_name} eq 'super' ) {
                    $sess->{perms}{super} = $permission->{p_id};
                }
            }

            # update last login info
            $model->upd_logins({ a_id => $account->{a_id} });

            $m->redirect('.');
            return;
        }
        else {
            $err = 'Sorry, username/password not found';
        }
    }
    else {
        unless ( $a_username eq'' and $a_password eq '' ) {
            $err = 'Please provide both your username and password';
        }
    }
</%init>
%# ----------------------------------------------------------------------------
<& view-sign-in.render.mhtml, username => $a_username, err => $err &>
%# ----------------------------------------------------------------------------
