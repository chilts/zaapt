%# ----------------------------------------------------------------------------
<%doc>
    Args:

    $p - page
</%doc>
%# ----------------------------------------------------------------------------
<%once>
    use POSIX;
    sub is_editable {
        my ($posts, $post, $forum, $id, $p) = @_;

        # not editable if a new post has been added (ie. this isn't the last post)
        return 0 unless $post == $posts->[-1];

        # not editable if this isn't your post
        return 0 unless $post->{p_account_id} == $id;

        # not editable unless the database says
        return 0 unless $post->{p_editable};

        # need to check if this is the last post of a topic

        # if we have less than we can show, then it must be the last post
        if ( @$posts < $forum->{f_show} ) {
            return 1;
        }

        # if we have the same as we can show, then it is either the last on a page or the last in total
        my $number_of_pages = ($post->{tp_posts}-1) / $forum->{f_show};
        $number_of_pages = POSIX::floor( $number_of_pages );
        $number_of_pages++;
        if ( $p == $number_of_pages ) {
            return 1;
        }

        return 0;
    }
</%once>
%# ----------------------------------------------------------------------------
<%args>
    $zaapt
    $a         => ''
    $f_name
    $tp_id
    $id        => undef
    $p_message => undef
    $t_id      => undef
    $p         => 1
    $last      => '9999-99-99 99:99:99'
</%args>
%# ----------------------------------------------------------------------------
<%once>
    my $allowed = {
        moderator => {
            'sticky-remove' => 1,
            'sticky-add'    => 1,
            'locked-add'    => 1,
            'locked-remove' => 1,
            'del'           => 1,
        },
    };
</%once>
%# ----------------------------------------------------------------------------
<%init>
    my $model = $zaapt->get_model('Forum');

    my $forum = $model->sel_forum_using_name({ f_name => $f_name} );
    unless ( defined $forum ) {
        die "Forum '$f_name' not found.";
        return;
    }

    my $topic = $model->sel_topic({ tp_id => $tp_id} );
    unless ( defined $topic and $topic->{f_id} == $forum->{f_id} ) {
        $m->clear_buffer();
        $m->abort(404);
        return;
    }

    $p = 1 unless $p =~ m{ \A \d+ \z }xms;

    # see if we want to insert a post
    my $err = [];
    if ( $a eq 'reply' ) {
        # firstly, check the error cases
        unless ( $SESS->{roles}{$forum->{_moderator}} or !$topic->{tp_locked} ) {
            $m->clear_buffer();
            $m->abort(404);
            return;
        }

        unless ( defined $id and defined $t_id ) {
            $m->clear_buffer();
            $m->redirect('?');
            return;
        }

        if ( $p_message =~ m{ \S }xms ) {
            $model->ins_post({ tp_id => $tp_id, a_id => $id, p_message => $p_message, t_id => $t_id });
            $p_message = '';
            $m->clear_buffer();
            $m->redirect("?p=$p");
            return;
        }
        else {
            push @$err, "Please type something into your reply";
        }
    }
    elsif ( exists $allowed->{moderator}{$a} ) {
        # these actions are just for moderators, so make sure they are one
        if ( exists $SESS->{roles}{$forum->{_moderator}} ) {
            if ( $a eq 'sticky-remove' ) {
                $model->upd_topic({ tp_id => $tp_id, tp_sticky => 0 });
            }
            elsif ( $a eq 'sticky-add' ) {
                $model->upd_topic({ tp_id => $tp_id, tp_sticky => 1 });
            }
            elsif ( $a eq 'locked-remove' ) {
                $model->upd_topic({ tp_id => $tp_id, tp_locked => 0 });
            }
            elsif ( $a eq 'locked-add' ) {
                $model->upd_topic({ tp_id => $tp_id, tp_locked => 1 });
            }
            elsif ( $a eq 'del' ) {
                $model->del_topic({ tp_id => $tp_id });
                $m->clear_buffer();
                $m->redirect('./');
                return;
            }
        }
        # redirect to the main page whatever happened above
        $m->clear_buffer();
        $m->redirect('?');
        return;
    }

    my $posts = $model->sel_all_posts_in({ tp_id => $tp_id, _offset => ($p-1) * $forum->{f_show}, _limit => $forum->{f_show} });
</%init>
%# ----------------------------------------------------------------------------
<& render-user-topic.mhtml,
    forum => $forum,
    topic => $topic,
    err => $err,
    p_message => $p_message,
    posts => $posts,
    id => $id,
    last => $last,
    p => $p,
&>
%# ----------------------------------------------------------------------------
