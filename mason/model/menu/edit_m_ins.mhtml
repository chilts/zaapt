%# ----------------------------------------------------------------------------
<%once>
    use Zaapt::Utils::Valid;
</%once>
%# ----------------------------------------------------------------------------
<%args>
    $zaapt
    $perms
    $m_name        => undef
    $m_title       => undef
    $m_description => undef
    $_admin        => undef
    $_view         => undef
    $_edit         => undef
</%args>
%# ----------------------------------------------------------------------------
<%init>
    my $model = $zaapt->get_model('Menu');

    # to insert this page, the user needs the 'super' role
    $m->redirect('edit_m_list.html') unless exists $perms->{super};

    my $err = [];

    unless ( Zaapt::Utils::Valid::is_valid_name($m_name) ) {
        push @$err, Zaapt::Utils::Valid::err();
    }

    # no Menu of this name should exist
    unless ( @$err ) {
        my $menu = $model->sel_menu_using_name({ m_name => $m_name });
        push @$err, "Menu '$m_name' already exists." if defined $menu;
    }

    $m_description =~ s{\r}{}gxms;

    unless ( @$err ) {
        $model->ins_menu({
            m_name        => $m_name,
            m_title       => $m_title,
            m_description => $m_description,
            _admin        => $_admin,
            _view         => $_view,
            _edit         => $_edit,
        });
        $m->redirect("edit_m_list.html");
        return;
    }

    my $options = $m->comp('/zaapt/common/permission_options.mhtml', zaapt => $zaapt);
</%init>
%# ----------------------------------------------------------------------------
<h1>Add a Menu</h1>

<p><a href="edit_m_list.html">&laquo; Back to Menu List</a></p>

<h2>Menu: ?</h2>

<& /zaapt/common/err.mhtml, err => $err &>

<& /zaapt/common/form.mhtml, action => 'edit_m_ins.html', elements => [
        { type => 'text', field => 'Name', name => 'm_name', size => 40, value => $m_name },
        { type => 'text', field => 'Title', name => 'm_title', size => 40, value => $m_title },
        { type => 'textarea', field => 'Description', name => 'm_description', rows => 10, cols => 40, value => $m_description },
        { type => 'select', field => 'Admin Perm', name => '_admin', options => $options, selected => $_admin },
        { type => 'select', field => 'View Perm', name => '_view', options => $options, selected => $_view },
        { type => 'select', field => 'Edit Perm', name => '_edit', options => $options, selected => $_edit },
        { type => 'submit', field => 'Actions', value => 'Add Menu', cancel => 'edit_m_list.html' },
    ]
&>

%# ----------------------------------------------------------------------------
